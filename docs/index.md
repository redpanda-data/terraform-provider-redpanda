---
page_title: "Provider: Redpanda"
description: |-
  The Redpanda Data Terraform provider is used to manage Redpanda Dedicated and Cloud clusters and Kafka resources within them. To connect to a Redpanda Cloud cluster, a client_id and client_secret are required.
---

# Redpanda Provider

The Redpanda provider is designed for managing Redpanda clusters and Kafka resources in Redpanda Dedicated and Cloud environments. It supports the provisioning, management, and configuration of clusters and Kafka resources, facilitating seamless integration into Terraform workflows.

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `access_token` (String, Sensitive) Redpanda client token. You need either `access_token`, or both `client_id` and `client_secret` to use this provider. Can also be set with the `REDPANDA_ACCESS_TOKEN` environment variable.
- `azure_client_id` (String) Used for creating and managing BYOC and BYOVPC clusters. Can also be specified in the environment as AZURE_CLIENT_ID or ARM_CLIENT_ID
- `azure_client_secret` (String) Used for creating and managing BYOC and BYOVPC clusters. Can also be specified in the environment as AZURE_CLIENT_SECRET or ARM_CLIENT_SECRET
- `azure_subscription_id` (String) The default Azure Subscription ID which should be used for Redpanda BYOC clusters. If another subscription is specified on a resource, it will take precedence. This can also be sourced from the `ARM_SUBSCRIPTION_ID` environment variable.
- `azure_tenant_id` (String) Used for creating and managing BYOC and BYOVPC clusters. Can also be specified in the environment as AZURE_TENANT_ID or ARM_TENANT_ID
- `client_id` (String, Sensitive) The ID for the client. You need either `client_id` AND `client_secret`, or `access_token`, to use this provider. Can also be set with the `REDPANDA_CLIENT_ID` environment variable.
- `client_secret` (String, Sensitive) Redpanda client secret. You need either `client_id` AND `client_secret`, or `access_token`, to use this provider. Can also be set with the `REDPANDA_CLIENT_SECRET` environment variable.
- `gcp_project_id` (String) The default Google Cloud Project ID to use for Redpanda BYOC clusters. If another project is specified on a resource, it will take precedence. This can also be sourced from the `GOOGLE_PROJECT` environment variable, or any of the following ordered by precedence: `GOOGLE_PROJECT`, `GOOGLE_CLOUD_PROJECT`, `GCLOUD_PROJECT`, or `CLOUDSDK_CORE_PROJECT`.
- `google_credentials` (String) Used for creating and managing BYOC and BYOVPC clusters. Can also be specified in the environment as GOOGLE_CREDENTIALS
- `google_credentials_base64` (String) Used for creating and managing BYOC and BYOVPC clusters. Is a convenience passthrough for base64 encoded credentials intended for use in CI/CD. Can also be specified in the environment as GOOGLE_CREDENTIALS_BASE64

## Authentication with Redpanda Cloud

This provider requires a `client_id` and `client_secret` for authentication with Redpanda Cloud services, enabling users to securely manage their Redpanda resources. You can get these by creating an account in [Redpanda Cloud](https://cloudv2.redpanda.com/home) and then [creating a client in the Redpanda Cloud UI](https://cloudv2.redpanda.com/clients).

## Example Provider Configuration

Terraform 1.0 or later:

```terraform
terraform {
  required_providers {
    redpanda = {
      source  = "redpanda-data/redpanda"
      version = "~> 0.2.4"
    }
  }
}

provider "redpanda" {
  client_id      = "your_client_id"
  client_secret  = "your_client_secret"
}
```

### Example Usage for an AWS Dedicated Cluster

```terraform
provider "redpanda" {}

resource "redpanda_resource_group" "test" {
  name = var.resource_group_name
}

resource "redpanda_network" "test" {
  name              = var.network_name
  resource_group_id = redpanda_resource_group.test.id
  cloud_provider    = var.cloud_provider
  region            = var.region
  cluster_type      = "dedicated"
  cidr_block        = "10.0.0.0/20"

  timeouts = {
    create = "20m"
    delete = "20m"
  }
}

resource "redpanda_cluster" "test" {
  name              = var.cluster_name
  resource_group_id = redpanda_resource_group.test.id
  network_id        = redpanda_network.test.id
  cloud_provider    = var.cloud_provider
  region            = var.region
  cluster_type      = "dedicated"
  connection_type   = "public"
  throughput_tier   = var.throughput_tier
  zones             = var.zones
  allow_deletion    = true
  cluster_configuration = {
    custom_properties_json = jsonencode({
      "schema_registry_enable_authorization" = true
    })
  }
  tags = {
    "key" = "value"
  }

  timeouts = {
    create = "90m"
  }
}

resource "redpanda_user" "test" {
  name            = var.user_name
  password        = var.user_pw
  mechanism       = var.mechanism
  cluster_api_url = redpanda_cluster.test.cluster_api_url
  allow_deletion  = var.user_allow_deletion
}


resource "redpanda_topic" "test" {
  name               = var.topic_name
  partition_count    = var.partition_count
  replication_factor = var.replication_factor
  cluster_api_url    = redpanda_cluster.test.cluster_api_url
  allow_deletion     = var.topic_allow_deletion
}


resource "redpanda_acl" "cluster_admin" {
  resource_type         = "CLUSTER"
  resource_name         = "kafka-cluster"
  resource_pattern_type = "LITERAL"
  principal             = "User:${redpanda_user.test.name}"
  host                  = var.acl_host
  operation             = "ALL"
  permission_type       = var.acl_permission_type
  cluster_api_url       = redpanda_cluster.test.cluster_api_url
  allow_deletion        = var.acl_allow_deletion
}

resource "redpanda_acl" "schema_registry_admin" {
  resource_type         = "CLUSTER"
  resource_name         = "kafka-cluster"
  resource_pattern_type = "LITERAL"
  principal             = "User:${redpanda_user.test.name}"
  host                  = var.acl_host
  operation             = "ALTER"
  permission_type       = var.acl_permission_type
  cluster_api_url       = redpanda_cluster.test.cluster_api_url
  allow_deletion        = var.acl_allow_deletion
}

resource "redpanda_acl" "cluster_action" {
  resource_type         = "CLUSTER"
  resource_name         = "kafka-cluster"
  resource_pattern_type = "LITERAL"
  principal             = "User:${redpanda_user.test.name}"
  host                  = var.acl_host
  operation             = "CLUSTER_ACTION"
  permission_type       = var.acl_permission_type
  cluster_api_url       = redpanda_cluster.test.cluster_api_url
  allow_deletion        = var.acl_allow_deletion
}

resource "redpanda_acl" "topic_access" {
  resource_type         = "TOPIC"
  resource_name         = var.topic_name
  resource_pattern_type = "LITERAL"
  principal             = "User:${redpanda_user.test.name}"
  host                  = var.acl_host
  operation             = "ALL"
  permission_type       = var.acl_permission_type
  cluster_api_url       = redpanda_cluster.test.cluster_api_url
  allow_deletion        = var.acl_allow_deletion
}

# Schema Registry ACLs
resource "redpanda_schema_registry_acl" "all_test_topic" {
  cluster_id     = redpanda_cluster.test.id
  principal      = "User:${redpanda_user.test.name}"
  resource_type  = "SUBJECT"
  resource_name  = "${var.topic_name}-"
  pattern_type   = "PREFIXED"
  host           = var.sr_acl_host
  operation      = "ALL"
  permission     = var.sr_acl_permission
  username       = redpanda_user.test.name
  password       = var.user_pw
  allow_deletion = var.sr_acl_allow_deletion
  depends_on     = [redpanda_acl.schema_registry_admin]
}

resource "redpanda_schema_registry_acl" "describe_registry" {
  cluster_id     = redpanda_cluster.test.id
  principal      = "User:${redpanda_user.test.name}"
  resource_type  = "REGISTRY"
  resource_name  = "*"
  pattern_type   = "LITERAL"
  host           = var.sr_acl_host
  operation      = "DESCRIBE"
  permission     = var.sr_acl_permission
  username       = redpanda_user.test.name
  password       = var.user_pw
  allow_deletion = var.sr_acl_allow_deletion

  depends_on = [redpanda_acl.schema_registry_admin]
}

resource "redpanda_schema_registry_acl" "alter_configs_registry" {
  cluster_id     = redpanda_cluster.test.id
  principal      = "User:${redpanda_user.test.name}"
  resource_type  = "REGISTRY"
  resource_name  = "*"
  pattern_type   = "LITERAL"
  host           = var.sr_acl_host
  operation      = "ALTER_CONFIGS"
  permission     = var.sr_acl_permission
  username       = redpanda_user.test.name
  password       = var.user_pw
  allow_deletion = var.sr_acl_allow_deletion

  depends_on = [redpanda_acl.schema_registry_admin]
}

resource "redpanda_schema" "user_schema" {
  cluster_id     = redpanda_cluster.test.id
  subject        = var.user_schema_subject != null ? var.user_schema_subject : "${var.topic_name}-value"
  schema_type    = var.schema_type
  schema         = var.user_schema_definition
  username       = redpanda_user.test.name
  password       = var.user_pw
  allow_deletion = var.schema_allow_deletion

  depends_on = [
    redpanda_acl.cluster_admin,
    redpanda_acl.schema_registry_admin,
    redpanda_acl.cluster_action,
    redpanda_acl.topic_access,
    redpanda_schema_registry_acl.all_test_topic,
    redpanda_schema_registry_acl.describe_registry,
    redpanda_schema_registry_acl.alter_configs_registry
  ]
}


resource "redpanda_schema" "user_event_schema" {
  cluster_id     = redpanda_cluster.test.id
  subject        = var.user_event_schema_subject != null ? var.user_event_schema_subject : "${var.topic_name}-events-value"
  schema_type    = var.schema_type
  schema         = var.user_event_schema_definition
  username       = redpanda_user.test.name
  password       = var.user_pw
  allow_deletion = var.schema_allow_deletion

  references = [
    {
      name    = "User"
      subject = redpanda_schema.user_schema.subject
      version = redpanda_schema.user_schema.version
    }
  ]

  depends_on = [
    redpanda_acl.cluster_admin,
    redpanda_acl.schema_registry_admin,
    redpanda_acl.cluster_action,
    redpanda_acl.topic_access,
    redpanda_schema_registry_acl.all_test_topic,
    redpanda_schema_registry_acl.describe_registry,
    redpanda_schema_registry_acl.alter_configs_registry
  ]
}

resource "redpanda_schema" "product_schema" {
  cluster_id     = redpanda_cluster.test.id
  subject        = var.product_schema_subject != null ? var.product_schema_subject : "${var.topic_name}-product-value"
  schema_type    = var.schema_type
  schema         = var.product_schema_definition
  compatibility  = var.compatibility_level
  username       = redpanda_user.test.name
  password       = var.user_pw
  allow_deletion = var.schema_allow_deletion

  depends_on = [
    redpanda_acl.cluster_admin,
    redpanda_acl.schema_registry_admin,
    redpanda_acl.cluster_action,
    redpanda_acl.topic_access,
    redpanda_schema_registry_acl.all_test_topic,
    redpanda_schema_registry_acl.describe_registry,
    redpanda_schema_registry_acl.alter_configs_registry
  ]
}

output "user_schema_info" {
  description = "Information about the created user schema"
  value = {
    id      = redpanda_schema.user_schema.id
    subject = redpanda_schema.user_schema.subject
    version = redpanda_schema.user_schema.version
    type    = redpanda_schema.user_schema.schema_type
  }
}

output "user_event_schema_info" {
  description = "Information about the created user event schema with references"
  value = {
    id         = redpanda_schema.user_event_schema.id
    subject    = redpanda_schema.user_event_schema.subject
    version    = redpanda_schema.user_event_schema.version
    type       = redpanda_schema.user_event_schema.schema_type
    references = redpanda_schema.user_event_schema.references
  }
}

output "product_schema_info" {
  description = "Information about the created product schema with compatibility settings"
  value = {
    id            = redpanda_schema.product_schema.id
    subject       = redpanda_schema.product_schema.subject
    version       = redpanda_schema.product_schema.version
    type          = redpanda_schema.product_schema.schema_type
    compatibility = redpanda_schema.product_schema.compatibility
  }
}
```

### Example Usage for a GCP Dedicated Cluster

```terraform
provider "redpanda" {}

resource "redpanda_resource_group" "test" {
  name = var.resource_group_name
}

resource "redpanda_network" "test" {
  name              = var.network_name
  resource_group_id = redpanda_resource_group.test.id
  cloud_provider    = var.cloud_provider
  region            = var.region
  cluster_type      = "dedicated"
  cidr_block        = "10.0.0.0/20"

  timeouts = {
    create = "20m"
    delete = "20m"
  }
}

resource "redpanda_cluster" "test" {
  name              = var.cluster_name
  resource_group_id = redpanda_resource_group.test.id
  network_id        = redpanda_network.test.id
  cloud_provider    = var.cloud_provider
  region            = var.region
  cluster_type      = "dedicated"
  connection_type   = "public"
  throughput_tier   = var.throughput_tier
  zones             = var.zones
  allow_deletion    = true
  cluster_configuration = {
    custom_properties_json = jsonencode({
      "schema_registry_enable_authorization" = true
    })
  }
  ## This is a reference for GCP tags
  #   tags = {
  #     "key" = "value"
  #   }
  ## This is a reference for GCP Private Service Connect
  #   gcp_private_service_connect = {
  #     enabled               = true
  #     global_access_enabled = true
  #     consumer_accept_list = [
  #       {
  #         source = "projects/123456789012"
  #       }
  #     ]
  #   }

  timeouts = {
    create = "90m"
  }
}

resource "redpanda_user" "test" {
  name            = var.user_name
  password        = var.user_pw
  mechanism       = var.mechanism
  cluster_api_url = redpanda_cluster.test.cluster_api_url
}

resource "redpanda_topic" "test" {
  name               = var.topic_name
  partition_count    = var.partition_count
  replication_factor = var.replication_factor
  cluster_api_url    = redpanda_cluster.test.cluster_api_url
  allow_deletion     = true
}


resource "redpanda_schema" "user_schema" {
  cluster_id  = redpanda_cluster.test.id
  subject     = "${var.topic_name}-value"
  schema_type = var.schema_type
  schema      = var.user_schema_definition
  username    = redpanda_user.test.name
  password    = var.user_pw
  allow_deletion = true
  
  depends_on = [
    redpanda_acl.schema_registry_admin,
    redpanda_schema_registry_acl.all_test_topic,
    redpanda_schema_registry_acl.describe_registry,
    redpanda_schema_registry_acl.alter_configs_registry
  ]
}

resource "redpanda_schema" "user_event_schema" {
  cluster_id  = redpanda_cluster.test.id
  subject     = "${var.topic_name}-events-value"
  schema_type = var.schema_type
  schema      = var.user_event_schema_definition
  username    = redpanda_user.test.name
  password    = var.user_pw
  allow_deletion = true
  
  references = [
    {
      name    = "User"
      subject = redpanda_schema.user_schema.subject
      version = redpanda_schema.user_schema.version
    }
  ]
  
  depends_on = [
    redpanda_acl.schema_registry_admin,
    redpanda_schema_registry_acl.all_test_topic,
    redpanda_schema_registry_acl.describe_registry,
    redpanda_schema_registry_acl.alter_configs_registry
  ]
}

resource "redpanda_schema" "product_schema" {
  cluster_id    = redpanda_cluster.test.id
  subject       = "${var.topic_name}-product-value"
  schema_type   = var.schema_type
  schema        = var.product_schema_definition
  compatibility = var.compatibility_level
  username      = redpanda_user.test.name
  password      = var.user_pw
  allow_deletion = true

  depends_on = [
    redpanda_acl.schema_registry_admin,
    redpanda_schema_registry_acl.all_test_topic,
    redpanda_schema_registry_acl.describe_registry,
    redpanda_schema_registry_acl.alter_configs_registry
  ]
}


resource "redpanda_acl" "cluster_admin" {
  resource_type         = "CLUSTER"
  resource_name         = "kafka-cluster"
  resource_pattern_type = "LITERAL"
  principal             = "User:${redpanda_user.test.name}"
  host                  = "*"
  operation             = "ALL"
  permission_type       = "ALLOW"
  cluster_api_url       = redpanda_cluster.test.cluster_api_url
  allow_deletion        = true
}

resource "redpanda_acl" "schema_registry_admin" {
  resource_type         = "CLUSTER"
  resource_name         = "kafka-cluster"
  resource_pattern_type = "LITERAL"
  principal             = "User:${redpanda_user.test.name}"
  host                  = "*"
  operation             = "ALTER"
  permission_type       = "ALLOW"
  cluster_api_url       = redpanda_cluster.test.cluster_api_url
  allow_deletion        = true
}

resource "redpanda_schema_registry_acl" "read_product" {
  cluster_id    = redpanda_cluster.test.id
  principal     = "User:${redpanda_user.test.name}"
  resource_type = "SUBJECT"
  resource_name = "product-"
  pattern_type  = "PREFIXED"
  host          = "*"
  operation     = "READ"
  permission    = "ALLOW"
  username      = redpanda_user.test.name
  password      = var.user_pw
  allow_deletion = true
  depends_on = [redpanda_acl.schema_registry_admin]
}

resource "redpanda_schema_registry_acl" "write_orders" {
  cluster_id    = redpanda_cluster.test.id
  principal     = "User:${redpanda_user.test.name}"
  resource_type = "SUBJECT"
  resource_name = "orders-value"
  pattern_type  = "LITERAL"
  host          = "*"
  operation     = "WRITE"
  permission    = "ALLOW"
  username      = redpanda_user.test.name
  password      = var.user_pw
  allow_deletion = true

  depends_on = [redpanda_acl.schema_registry_admin]
}

resource "redpanda_schema_registry_acl" "all_test_topic" {
  cluster_id    = redpanda_cluster.test.id
  principal     = "User:${redpanda_user.test.name}"
  resource_type = "SUBJECT"
  resource_name = "${var.topic_name}-"
  pattern_type  = "PREFIXED"
  host          = "*"
  operation     = "ALL"
  permission    = "ALLOW"
  username      = redpanda_user.test.name
  password      = var.user_pw
  allow_deletion = true
  
  depends_on = [redpanda_acl.schema_registry_admin]
}

resource "redpanda_schema_registry_acl" "describe_test_topic" {
  cluster_id    = redpanda_cluster.test.id
  principal     = "User:${redpanda_user.test.name}"
  resource_type = "SUBJECT"
  resource_name = "${var.topic_name}-"
  pattern_type  = "PREFIXED"
  host          = "*"
  operation     = "DESCRIBE"
  permission    = "ALLOW"
  username      = redpanda_user.test.name
  password      = var.user_pw
  allow_deletion = true
  
  depends_on = [redpanda_acl.schema_registry_admin]
}

resource "redpanda_schema_registry_acl" "describe_registry" {
  cluster_id    = redpanda_cluster.test.id
  principal     = "User:${redpanda_user.test.name}"
  resource_type = "REGISTRY"
  resource_name = "*"
  pattern_type  = "LITERAL"
  host          = "*"
  operation     = "DESCRIBE"
  permission    = "ALLOW"
  username      = redpanda_user.test.name
  password      = var.user_pw
  allow_deletion = true
  
  depends_on = [redpanda_acl.schema_registry_admin]
}

resource "redpanda_schema_registry_acl" "alter_configs_registry" {
  cluster_id    = redpanda_cluster.test.id
  principal     = "User:${redpanda_user.test.name}"
  resource_type = "REGISTRY"
  resource_name = "*"
  pattern_type  = "LITERAL"
  host          = "*"
  operation     = "ALTER_CONFIGS"
  permission    = "ALLOW"
  username      = redpanda_user.test.name
  password      = var.user_pw
  allow_deletion = true

  depends_on = [redpanda_acl.schema_registry_admin]
}

output "user_schema_info" {
  description = "Information about the created user schema"
  value = {
    id      = redpanda_schema.user_schema.id
    subject = redpanda_schema.user_schema.subject
    version = redpanda_schema.user_schema.version
    type    = redpanda_schema.user_schema.schema_type
  }
}

output "user_event_schema_info" {
  description = "Information about the created user event schema with references"
  value = {
    id         = redpanda_schema.user_event_schema.id
    subject    = redpanda_schema.user_event_schema.subject
    version    = redpanda_schema.user_event_schema.version
    type       = redpanda_schema.user_event_schema.schema_type
    references = redpanda_schema.user_event_schema.references
  }
}

output "product_schema_info" {
  description = "Information about the created product schema with compatibility settings"
  value = {
    id            = redpanda_schema.product_schema.id
    subject       = redpanda_schema.product_schema.subject
    version       = redpanda_schema.product_schema.version
    type          = redpanda_schema.product_schema.schema_type
    compatibility = redpanda_schema.product_schema.compatibility
  }
}
```

### Example Usage of a data source BYOC to manage users and ACLs

```terraform
provider "redpanda" {}

data "redpanda_cluster" "test" {
  id = var.cluster_id
}

resource "redpanda_user" "test" {
  name            = var.user_name
  password        = var.user_pw
  mechanism       = var.mechanism
  cluster_api_url = data.redpanda_cluster.test.cluster_api_url
  allow_deletion  = var.user_allow_deletion
}

resource "redpanda_user" "test_user" {
  name            = var.test_user_name
  password        = var.test_user_pw
  mechanism       = var.test_user_mechanism
  cluster_api_url = data.redpanda_cluster.test.cluster_api_url
  allow_deletion  = var.user_allow_deletion
}

resource "redpanda_topic" "test" {
  name               = var.topic_name
  partition_count    = var.partition_count
  replication_factor = var.replication_factor
  cluster_api_url    = data.redpanda_cluster.test.cluster_api_url
  allow_deletion     = var.topic_allow_deletion
}

resource "redpanda_schema" "user_schema" {
  cluster_id     = data.redpanda_cluster.test.id
  subject        = var.user_schema_subject != null ? var.user_schema_subject : "${var.topic_name}-value"
  schema_type    = var.schema_type
  schema         = var.user_schema_definition
  username       = redpanda_user.test.name
  password       = var.user_pw
  allow_deletion = var.schema_allow_deletion

  depends_on = [
    redpanda_acl.cluster_admin,
    redpanda_acl.schema_registry_admin,
    redpanda_acl.cluster_action,
    redpanda_acl.topic_access,
    redpanda_schema_registry_acl.all_test_topic,
    redpanda_schema_registry_acl.describe_registry,
    redpanda_schema_registry_acl.alter_configs_registry
  ]
}

resource "redpanda_schema" "user_event_schema" {
  cluster_id     = data.redpanda_cluster.test.id
  subject        = var.user_event_schema_subject != null ? var.user_event_schema_subject : "${var.topic_name}-events-value"
  schema_type    = var.schema_type
  schema         = var.user_event_schema_definition
  username       = redpanda_user.test.name
  password       = var.user_pw
  allow_deletion = var.schema_allow_deletion

  references = [
    {
      name    = "User"
      subject = redpanda_schema.user_schema.subject
      version = redpanda_schema.user_schema.version
    }
  ]

  depends_on = [
    redpanda_acl.cluster_admin,
    redpanda_acl.schema_registry_admin,
    redpanda_acl.cluster_action,
    redpanda_acl.topic_access,
    redpanda_schema_registry_acl.all_test_topic,
    redpanda_schema_registry_acl.describe_registry,
    redpanda_schema_registry_acl.alter_configs_registry
  ]
}

resource "redpanda_schema" "product_schema" {
  cluster_id     = data.redpanda_cluster.test.id
  subject        = var.product_schema_subject != null ? var.product_schema_subject : "${var.topic_name}-product-value"
  schema_type    = var.schema_type
  schema         = var.product_schema_definition
  compatibility  = var.compatibility_level
  username       = redpanda_user.test.name
  password       = var.user_pw
  allow_deletion = var.schema_allow_deletion

  depends_on = [
    redpanda_acl.cluster_admin,
    redpanda_acl.schema_registry_admin,
    redpanda_acl.cluster_action,
    redpanda_acl.topic_access,
    redpanda_schema_registry_acl.all_test_topic,
    redpanda_schema_registry_acl.describe_registry,
    redpanda_schema_registry_acl.alter_configs_registry
  ]
}

resource "redpanda_acl" "cluster_admin" {
  resource_type         = "CLUSTER"
  resource_name         = "kafka-cluster"
  resource_pattern_type = "LITERAL"
  principal             = "User:${redpanda_user.test.name}"
  host                  = var.acl_host
  operation             = "ALL"
  permission_type       = var.acl_permission_type
  cluster_api_url       = data.redpanda_cluster.test.cluster_api_url
  allow_deletion        = var.acl_allow_deletion
}

resource "redpanda_acl" "schema_registry_admin" {
  resource_type         = "CLUSTER"
  resource_name         = "kafka-cluster"
  resource_pattern_type = "LITERAL"
  principal             = "User:${redpanda_user.test.name}"
  host                  = var.acl_host
  operation             = "ALTER"
  permission_type       = var.acl_permission_type
  cluster_api_url       = data.redpanda_cluster.test.cluster_api_url
  allow_deletion        = var.acl_allow_deletion
}

resource "redpanda_acl" "cluster_action" {
  resource_type         = "CLUSTER"
  resource_name         = "kafka-cluster"
  resource_pattern_type = "LITERAL"
  principal             = "User:${redpanda_user.test.name}"
  host                  = var.acl_host
  operation             = "CLUSTER_ACTION"
  permission_type       = var.acl_permission_type
  cluster_api_url       = data.redpanda_cluster.test.cluster_api_url
  allow_deletion        = var.acl_allow_deletion
}

resource "redpanda_acl" "topic_access" {
  resource_type         = "TOPIC"
  resource_name         = var.topic_name
  resource_pattern_type = "LITERAL"
  principal             = "User:${redpanda_user.test.name}"
  host                  = var.acl_host
  operation             = "ALL"
  permission_type       = var.acl_permission_type
  cluster_api_url       = data.redpanda_cluster.test.cluster_api_url
  allow_deletion        = var.acl_allow_deletion
}

# Schema Registry ACLs
resource "redpanda_schema_registry_acl" "all_test_topic" {
  cluster_id     = data.redpanda_cluster.test.id
  principal      = "User:${redpanda_user.test.name}"
  resource_type  = "SUBJECT"
  resource_name  = "${var.topic_name}-"
  pattern_type   = "PREFIXED"
  host           = var.sr_acl_host
  operation      = "ALL"
  permission     = var.sr_acl_permission
  username       = redpanda_user.test.name
  password       = var.user_pw
  allow_deletion = var.sr_acl_allow_deletion
  depends_on     = [redpanda_acl.schema_registry_admin]
}

resource "redpanda_schema_registry_acl" "describe_registry" {
  cluster_id     = data.redpanda_cluster.test.id
  principal      = "User:${redpanda_user.test.name}"
  resource_type  = "REGISTRY"
  resource_name  = "*"
  pattern_type   = "LITERAL"
  host           = var.sr_acl_host
  operation      = "DESCRIBE"
  permission     = var.sr_acl_permission
  username       = redpanda_user.test.name
  password       = var.user_pw
  allow_deletion = var.sr_acl_allow_deletion

  depends_on = [redpanda_acl.schema_registry_admin]
}

resource "redpanda_schema_registry_acl" "alter_configs_registry" {
  cluster_id     = data.redpanda_cluster.test.id
  principal      = "User:${redpanda_user.test.name}"
  resource_type  = "REGISTRY"
  resource_name  = "*"
  pattern_type   = "LITERAL"
  host           = var.sr_acl_host
  operation      = "ALTER_CONFIGS"
  permission     = var.sr_acl_permission
  username       = redpanda_user.test.name
  password       = var.user_pw
  allow_deletion = var.sr_acl_allow_deletion

  depends_on = [redpanda_acl.schema_registry_admin]
}

output "user_schema_info" {
  description = "Information about the created user schema"
  value = {
    id      = redpanda_schema.user_schema.id
    subject = redpanda_schema.user_schema.subject
    version = redpanda_schema.user_schema.version
    type    = redpanda_schema.user_schema.schema_type
  }
}

output "user_event_schema_info" {
  description = "Information about the created user event schema with references"
  value = {
    id         = redpanda_schema.user_event_schema.id
    subject    = redpanda_schema.user_event_schema.subject
    version    = redpanda_schema.user_event_schema.version
    type       = redpanda_schema.user_event_schema.schema_type
    references = redpanda_schema.user_event_schema.references
  }
}

output "product_schema_info" {
  description = "Information about the created product schema with compatibility settings"
  value = {
    id            = redpanda_schema.product_schema.id
    subject       = redpanda_schema.product_schema.subject
    version       = redpanda_schema.product_schema.version
    type          = redpanda_schema.product_schema.schema_type
    compatibility = redpanda_schema.product_schema.compatibility
  }
}
```

### Example Usage to create a serverless cluster

```terraform
provider "redpanda" {
}

resource "redpanda_resource_group" "test" {
  name = var.resource_group_name
}

resource "redpanda_serverless_cluster" "test" {
  name              = var.cluster_name
  resource_group_id = redpanda_resource_group.test.id
  serverless_region = var.region
}

resource "redpanda_topic" "test" {
  name               = var.topic_name
  partition_count    = var.partition_count
  replication_factor = var.replication_factor
  cluster_api_url    = redpanda_serverless_cluster.test.cluster_api_url
  allow_deletion     = true
}

resource "redpanda_user" "test" {
  name            = var.user_name
  password        = var.user_pw
  mechanism       = var.mechanism
  cluster_api_url = redpanda_serverless_cluster.test.cluster_api_url
  allow_deletion  = true
}
```
