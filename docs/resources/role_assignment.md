---
page_title: "redpanda_role_assignment Resource - terraform-provider-redpanda"
subcategory: ""
description: |-
  A role assignment is used for attaching one user to a role.
---

# redpanda_role_assignment (Resource)

Assigns an existing Redpanda role to a principal. Resource ID format: `{role_name}:{principal}`

## Example Usage

```terraform
provider "redpanda" {}

resource "redpanda_resource_group" "example" {
  name = "example-resource-group"
}

resource "redpanda_network" "example" {
  name              = "example-network"
  resource_group_id = redpanda_resource_group.example.id
  cloud_provider    = "aws"
  region            = "us-west-2"
  cluster_type      = "dedicated"
  cidr_block        = "10.0.0.0/20"
}

resource "redpanda_cluster" "example" {
  name              = "example-cluster"
  resource_group_id = redpanda_resource_group.example.id
  network_id        = redpanda_network.example.id
  cloud_provider    = "aws"
  region            = "us-west-2"
  cluster_type      = "dedicated"
  connection_type   = "public"
  throughput_tier   = "tier-1-aws"
  zones             = ["us-west-2a", "us-west-2b", "us-west-2c"]
}

resource "redpanda_user" "example" {
  name            = "example-user"
  password        = "secure-password-123"
  mechanism       = "scram-sha-256"
  cluster_api_url = redpanda_cluster.example.cluster_api_url
  allow_deletion  = true
}

resource "redpanda_role_assignment" "example" {
  role_name       = "test-role"
  principal       = redpanda_user.example.name
  cluster_api_url = redpanda_cluster.example.cluster_api_url
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `cluster_api_url` (String) The cluster API URL. Changing this will prevent deletion of the resource on the existing cluster
- `principal` (String) The principal to assign the role to. Specify just the username (e.g., `"john.doe"`)
- `role_name` (String) The name of the role to assign

### Read-Only

- `id` (String) The ID of this resource. Format: `{role_name}:{principal}`

## Import

Role assignments can be imported using the format `role_name:principal`:

```shell
terraform import redpanda_role_assignment.test "test-role:test-user"
```

Note: The `cluster_api_url` must be specified in your Terraform configuration. The import will validate the role assignment exists during the next `terraform plan` or `terraform apply`.

## Notes

- The role must already exist before it can be assigned. Create roles using the `redpanda_role` resource, or import existing roles created via `rpk` or Redpanda Console.
- The principal should be specified as just the username (e.g., `"john.doe"`). While Redpanda's native format is `User:username`, this resource automatically handles the `User:` prefix for you. The prefix will be automatically stripped if provided.
- A user can have multiple role assignments simultaneously. Permissions from all assigned roles are combined.
- Role assignments are atomic operations - you cannot update an existing assignment. To change a role assignment, delete and recreate the resource.
- The resource uses the Redpanda gRPC SecurityService (via console endpoint) for role management operations.

## API Reference

For more information, see:
- [Redpanda RBAC in Data Plane](https://docs.redpanda.com/redpanda-cloud/security/authorization/rbac/rbac_dp/)
- [Redpanda Cloud Data Plane API](https://docs.redpanda.com/api/cloud-dataplane-api/)