---
page_title: "redpanda_schema_registry_acl Resource - terraform-provider-redpanda"
subcategory: ""
description: |-
  Resource for managing Redpanda Schema Registry ACLs (Access Control Lists). This resource allows you to configure fine-grained access control for Schema Registry resources.
---

# redpanda_schema_registry_acl (Resource)

Resource for managing Redpanda Schema Registry ACLs (Access Control Lists). This resource allows you to configure fine-grained access control for Schema Registry resources.

Creates Access Control Lists (ACLs) for Redpanda Schema Registry resources. Schema Registry ACLs provide fine-grained access control over subjects and registry operations.

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `cluster_id` (String) The ID of the cluster where the Schema Registry ACL will be created
- `host` (String) The host address to use for this ACL. Use '*' for wildcard
- `operation` (String) The operation type that shall be allowed or denied: ALL, READ, WRITE, DELETE, DESCRIBE, DESCRIBE_CONFIGS, ALTER, ALTER_CONFIGS
- `pattern_type` (String) The pattern type of the resource: LITERAL or PREFIXED
- `permission` (String) The permission type: ALLOW or DENY
- `principal` (String) The principal to apply this ACL for (e.g., User:alice or RedpandaRole:admin)
- `resource_name` (String) The name of the resource this ACL entry will be on. Use '*' for wildcard
- `resource_type` (String) The type of the resource: SUBJECT or REGISTRY

### Optional

> **NOTE**: [Write-only arguments](https://developer.hashicorp.com/terraform/language/resources/ephemeral#write-only-arguments) are supported in Terraform 1.11 and later.

- `allow_deletion` (Boolean) When set to true, allows the resource to be removed from state even if deletion fails due to permission errors
- `password` (String, Sensitive, Deprecated) Password for authentication. Deprecated: use password_wo instead. Can be set via REDPANDA_SR_PASSWORD environment variable
- `password_wo` (String, [Write-only](https://developer.hashicorp.com/terraform/language/resources/ephemeral#write-only-arguments)) Password for authentication (write-only, not stored in state). Requires Terraform 1.11+. Can be set via REDPANDA_SR_PASSWORD environment variable
- `password_wo_version` (Number) Version number for password_wo. Increment this value to trigger a password update when using password_wo.
- `username` (String, Sensitive) Username for authentication. Can be set via REDPANDA_SR_USERNAME environment variable

### Read-Only

- `id` (String) The ID of this resource.

## Example Usage

```terraform
provider "redpanda" {}

variable "user_password" {
  type        = string
  sensitive   = true
  description = "Password for the Redpanda user and Schema Registry ACL authentication"
}

resource "redpanda_resource_group" "example" {
  name = "example-resource-group"
}

resource "redpanda_network" "example" {
  name              = "example-network"
  resource_group_id = redpanda_resource_group.example.id
  cloud_provider    = "aws"
  region            = "us-west-2"
  cluster_type      = "dedicated"
  cidr_block        = "10.0.0.0/20"
}

resource "redpanda_cluster" "example" {
  name              = "example-cluster"
  resource_group_id = redpanda_resource_group.example.id
  network_id        = redpanda_network.example.id
  cloud_provider    = "aws"
  region            = "us-west-2"
  cluster_type      = "dedicated"
  connection_type   = "public"
  throughput_tier   = "tier-1-aws"
  zones             = ["us-west-2a", "us-west-2b", "us-west-2c"]
}

resource "redpanda_user" "example" {
  name                = "schema-user"
  password_wo         = var.user_password # Write-only, not stored in state
  password_wo_version = 1                 # Increment to trigger password update
  mechanism           = "scram-sha-256"
  cluster_api_url     = redpanda_cluster.example.cluster_api_url
  allow_deletion      = true
}

resource "redpanda_acl" "schema_registry_admin" {
  resource_type         = "CLUSTER"
  resource_name         = "kafka-cluster"
  resource_pattern_type = "LITERAL"
  principal             = "User:${redpanda_user.example.name}"
  host                  = "*"
  operation             = "ALTER"
  permission_type       = "ALLOW"
  cluster_api_url       = redpanda_cluster.example.cluster_api_url
}

resource "redpanda_schema_registry_acl" "example" {
  cluster_id          = redpanda_cluster.example.id
  principal           = "User:${redpanda_user.example.name}"
  resource_type       = "SUBJECT"
  resource_name       = "user-value"
  pattern_type        = "LITERAL"
  host                = "*"
  operation           = "READ"
  permission          = "ALLOW"
  username            = redpanda_user.example.name
  password_wo         = var.user_password # Write-only, not stored in state
  password_wo_version = 1                 # Increment to trigger password update

  depends_on = [redpanda_acl.schema_registry_admin]
}
```

## Resource Types

Schema Registry ACLs support two resource types:

### SUBJECT
Controls access to specific subjects or subject patterns in the Schema Registry. Use this for schema-level permissions.

### REGISTRY  
Controls access to registry-wide operations like listing subjects or getting compatibility settings.

## Operations

Available operations depend on the resource type:

- `ALL`: All operations (use with caution)
- `READ`: Read schemas and subject metadata
- `WRITE`: Create and update schemas
- `DELETE`: Delete schemas and subjects
- `DESCRIBE`: View subject and schema metadata
- `DESCRIBE_CONFIGS`: View compatibility and other configuration settings
- `ALTER`: Modify subject settings
- `ALTER_CONFIGS`: Modify compatibility and other configuration settings

## Pattern Types

- `LITERAL`: Exact match on resource name
- `PREFIXED`: Match resources that start with the specified prefix

## Administrative Requirements

To manage Schema Registry ACLs, the user must have cluster-level ALTER permissions. This is typically configured with a regular Kafka ACL granting ALTER operation on the CLUSTER resource.

For more details about Schema Registry ACLs, see the [Redpanda Schema Registry API documentation](https://docs.redpanda.com/current/manage/schema-reg/schema-reg-api/).

## Security Considerations

We recommend storing Schema Registry credentials in environment variables or a secret store:

- `REDPANDA_SR_USERNAME` for the username
- `REDPANDA_SR_PASSWORD` for the password

### Write-Only Password (Recommended)

For Terraform 1.11+, we recommend using the `password_wo` attribute instead of `password`. Write-only attributes are never stored in Terraform state, providing enhanced security:

```hcl
resource "redpanda_schema_registry_acl" "example" {
  cluster_id          = redpanda_cluster.example.id
  principal           = "User:alice"
  resource_type       = "SUBJECT"
  resource_name       = "user-value"
  pattern_type        = "LITERAL"
  host                = "*"
  operation           = "READ"
  permission          = "ALLOW"
  username            = "admin-user"
  password_wo         = var.sr_password  # Not stored in state
  password_wo_version = 1                # Increment to trigger password update
}
```

To update the password, change the `password_wo` value and increment `password_wo_version`. The version field signals Terraform that the password has changed since write-only values cannot be compared between plan and apply.

~> **Note:** The `password` attribute is deprecated and will be removed in a future version. Migrate to `password_wo` when using Terraform 1.11+.

## Import

Schema Registry ACLs can be imported using a colon-separated string with the following format:

```
cluster_id:principal:resource_type:resource_name:pattern_type:host:operation:permission:username:password
```

Example imports:

```shell
# Import a subject-level READ ACL for user alice
terraform import redpanda_schema_registry_acl.example "cluster-123:User:alice:SUBJECT:user-value:LITERAL:*:READ:ALLOW:alice:mypassword"

# Import a registry-level ALL ACL for a role
terraform import redpanda_schema_registry_acl.admin "cluster-123:RedpandaRole:admin:REGISTRY:*:LITERAL:*:ALL:ALLOW:admin:secret"

# Import a prefixed pattern ACL
terraform import redpanda_schema_registry_acl.prefix "cluster-123:User:bob:SUBJECT:orders-:PREFIXED:*:WRITE:ALLOW:bob:pass123"
```

**Note:** Principals containing colons (e.g., `RedpandaRole:admin:extra`) are supported in the import format. Credentials (username and password) are required for import to verify the ACL exists.

## API Reference

For more information, see the [Redpanda Schema Registry API documentation](https://docs.redpanda.com/current/manage/schema-reg/schema-reg-api/) and the [Redpanda Cloud Data Plane API documentation](https://docs.redpanda.com/api/cloud-dataplane-api/).