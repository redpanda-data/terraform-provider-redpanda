---
page_title: "redpanda_user Resource - terraform-provider-redpanda"
subcategory: ""
description: |-
  User is a user that can be created in Redpanda
---

# redpanda_user (Resource)

User is a user that can be created in Redpanda

Creates a user in a Redpanda Cluster.

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `cluster_api_url` (String) The cluster API URL. Changing this will prevent deletion of the resource on the existing cluster. It is generally a better idea to delete an existing resource and create a new one than to change this value unless you are planning to do state imports
- `name` (String) Name of the user, must be unique

### Optional

> **NOTE**: [Write-only arguments](https://developer.hashicorp.com/terraform/language/resources/ephemeral#write-only-arguments) are supported in Terraform 1.11 and later.

- `allow_deletion` (Boolean) Allows deletion of the user. If false, the user cannot be deleted and the resource will be removed from the state on destruction. Defaults to false.
- `mechanism` (String) Which authentication method to use, see https://docs.redpanda.com/current/manage/security/authentication/ for more information
- `password` (String, Sensitive, Deprecated) Password of the user. Deprecated: use password_wo instead to avoid storing password in state.
- `password_wo` (String, [Write-only](https://developer.hashicorp.com/terraform/language/resources/ephemeral#write-only-arguments)) Password of the user (write-only, not stored in state). Requires Terraform 1.11+. Either password or password_wo must be set.
- `password_wo_version` (Number) Version number for password_wo. Increment this value to trigger a password update when using password_wo.

### Read-Only

- `id` (String) The ID of this resource.

## Example Usage

```terraform
provider "redpanda" {}

variable "user_password" {
  type        = string
  sensitive   = true
  description = "Password for the Redpanda user"
}

resource "redpanda_resource_group" "example" {
  name = "example-resource-group"
}

resource "redpanda_network" "example" {
  name              = "example-network"
  resource_group_id = redpanda_resource_group.example.id
  cloud_provider    = "aws"
  region            = "us-west-2"
  cluster_type      = "dedicated"
  cidr_block        = "10.0.0.0/20"
}

resource "redpanda_cluster" "example" {
  name              = "example-cluster"
  resource_group_id = redpanda_resource_group.example.id
  network_id        = redpanda_network.example.id
  cloud_provider    = "aws"
  region            = "us-west-2"
  cluster_type      = "dedicated"
  connection_type   = "public"
  throughput_tier   = "tier-1-aws"
  zones             = ["us-west-2a", "us-west-2b", "us-west-2c"]
}

resource "redpanda_user" "example" {
  name                = "example-user"
  password_wo         = var.user_password # Write-only, not stored in state
  password_wo_version = 1                 # Increment to trigger password update
  mechanism           = "scram-sha-256"
  cluster_api_url     = redpanda_cluster.example.cluster_api_url
  allow_deletion      = true
}
```

## Security Considerations

We recommend storing user passwords in a secret store and only passing them in at runtime. They are marked sensitive and will not be shown in plan or apply output.

However it is worth remembering that [marking a field sensitive](https://developer.hashicorp.com/terraform/plugin/best-practices/sensitive-state) does not obscure it or encrypt it in state and that terraform offers limited support on this matter.

Be cognizant of the security implications of storing sensitive data in state and consider using a secure state backend for your state files.

### Write-Only Password (Recommended)

For Terraform 1.11+, we recommend using the `password_wo` attribute instead of `password`. Write-only attributes are never stored in Terraform state, providing enhanced security:

```hcl
resource "redpanda_user" "example" {
  name                = "example-user"
  password_wo         = var.user_password  # Not stored in state
  password_wo_version = 1                  # Increment to trigger password update
  mechanism           = "scram-sha-256"
  cluster_api_url     = redpanda_cluster.example.cluster_api_url
}
```

To update the password, change the `password_wo` value and increment `password_wo_version`. The version field signals Terraform that the password has changed since write-only values cannot be compared between plan and apply.

~> **Note:** The `password` attribute is deprecated and will be removed in a future version. Migrate to `password_wo` when using Terraform 1.11+.

## Limitations

We are not currently able to support user creation in self hosted clusters. This is an area of active development so expect that to change soon.

## Import

```shell
# Basic import with just user name and cluster ID
terraform import redpanda_user.example userName,clusterId

# Extended import with password
terraform import redpanda_user.example userName,clusterId,password

# Extended import with password and mechanism
terraform import redpanda_user.example userName,clusterId,password,mechanism
```

Where:
- `userName` is the name of the user
- `clusterId` is the ID of the cluster in Redpanda Cloud
- `password` (optional) is the user's password to set in state after import
- `mechanism` (optional) is the authentication mechanism (e.g., `SCRAM-SHA-256`) and can only be passed in with password

## API Reference

For more information, see the [Redpanda Cloud Data Plane API documentation](https://docs.redpanda.com/api/cloud-dataplane-api/).
